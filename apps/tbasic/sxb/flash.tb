REM ***********************
REM ** SBX Flash Utility **
REM ***********************

100 PR ""
105 DO
110   PR "SXB Flash Utility"
115   PR " 0: Quit"
120   PR " 1: Write GIBL    $6000 -> $E000"
130   PR " 2: Write SXB-MON $7000 -> $F000"
135   PR " 3: Reboot to Monitor"
140   PR "Select: ";
150   C=GET-48
160 UNTIL  (C<4) AND (C>= 0)
170 GOSUB 200 + C*100 : GOTO 100

200 PR "Quit":END

300 PR "Write GIBL:"
310 S = 6:GOSUB 900
320 A=&6000 : B=&E000 : C=&1000 : D=0: GOTO 800

400 PR "Write MON:"
410 S=7 : GOSUB 900
420 A=&7000 : B=&F000 : C=&1000 : D=0 : GOTO 800

500 MON : END

800 REM Do Flash
810 PR ". Flash";
820 LED=15 : FLA : LED = 0 
830 PR ". Done."
840 RETURN

900 REM Erase a sector: S
910 D = S / 8 : A = S % 8 * 4096 + &8000
920 PR "  Erase B:", D, "/S:", ~A;
930 LED = D + 1 : ERA : LED = 0
940 RETURN
